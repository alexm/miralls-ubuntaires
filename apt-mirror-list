#!/usr/bin/env perl
#
# apt-mirror-list
#

use strict;
use warnings;
use 5.010;

my %conf = do "$0.conf";
die "$0: could not parse $0.conf: $@\n" if $@;
die "$0: config error in $0.conf\n" unless %conf;

# header
print <DATA>;

local $, = q( );

say "clean $conf{uri}";
for my $dist ( sort keys %{ $conf{dists} } ) {
    for my $arch ( sort keys %{ $conf{deb} } ) {
        for my $component ( @{ $conf{components} } ) {
            say $conf{deb}{$arch}, $conf{uri}, $dist, $component;
            for my $extra ( @{ $conf{extra} } ) {
                say $conf{deb}{$arch}, $conf{uri}, "$dist$extra", $component
            }
        }
        for my $extra ( sort keys %{ $conf{'debian-installer'} } ) {
            # debian-installer available only in main for unreleased dists
            next if !$conf{dists}{$dist} and $extra ne '';
            for my $component ( @{ $conf{'debian-installer'}{$extra} } ) {
                say $conf{deb}{$arch}, $conf{uri}, "$dist$extra", "$component/debian-installer";
            }
        }
    }
}

__DATA__
############# config ##################
#
# set base_path    /var/spool/apt-mirror
#
# if you change the base path you must create the directories below with write privileges
#
# set mirror_path  $base_path/mirror
# set skel_path    $base_path/skel
# set var_path     $base_path/var
# set cleanscript $var_path/clean.sh
set defaultarch i386
# set postmirror_script $var_path/postmirror.sh
set run_postmirror 0
set nthreads     20
set _tilde 0
#
############# end config ##############

